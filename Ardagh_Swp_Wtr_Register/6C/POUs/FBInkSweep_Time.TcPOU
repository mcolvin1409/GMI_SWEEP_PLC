<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.10">
  <POU Name="FBInkSweep_Time" Id="{c7b5d05f-c237-4bbc-88e1-6a4556ad0af2}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FBInkSweep_Time
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
///SDOC/////////////////////////////////////////////////////////////////////////
//
//  FBInkSweep_Time.TcPOU      - Function Block - Ink positioner control by motor on time - AVT PLC 
//															Version 1 - NOT COMPLETE YET   **************
//  Author:         Mark Colvin                        Date: Apr 20, 2018
//
//	Beckhoff TC3 Structured Text for CX50x0 PLC
//
//	includes features:
//		10 inker enables
//		Ink ramping
//		Water ramping
//		Ink Surge
//		Water Flood
//		Ink Ducting (Hold Off)
//		Ink Wash Up 
//		Ink Function Control (Off/manual/auto)
//		water Function Control (off/manual/auto)
//		Inker positioner with feedback
//		Water positioner with feedback
//		Inker Analog Control
//		water Analog Control
//		Inker Positioner output cancel
//		Water Positioner output cancel
///////////////////////////////////////////////////////////////////////SDOCEND//
	// === LOCAL DATA ===
	
	//FB_PlusMinusByTime_arr	:FB_PlusMinusByTime;  //debug the FB_PlusMinusByTime FB, only single ftn with one set of locals
	FB_PlusMinusByTime_arr	:ARRAY[0..GVL.w_MAXFOUNTAINS] OF FB_PlusMinusByTime;  //normal operations, each fountain has own local variables
	nScreenValceu				:WORD;								//???
	Index					:WORD;								// temp word for loop fountain index
	Index_offset			:WORD;								//index offset to runtime data for fountains 7 to 10; 0 or 220
	w_itrgt					:WORD;								// temp index for target sweep % in MB array
	w_icmpr					:WORD;								//temp index for on-press sweep % in MB array
	w_Actual					:WORD;						//temp A2D FB value
	bError					:ARRAY [0..GVL.w_MAXFOUNTAINS] OF BOOL;	//error recorded from FB_PlusMinusByTime FB
	wError					:WORD;							//temp error code to process later
	bRESET					:ARRAY [0..GVL.w_MAXFOUNTAINS] OF BOOL;	//request to FB_PlusMinusByTime FB to reset any error seen previously
	w_halfpercent				:ARRAY [0..GVL.w_MAXFOUNTAINS] OF WORD;	// 1 percent ticks halved
	w_target_ticks			:ARRAY [0..GVL.w_MAXFOUNTAINS] OF WORD;	// current target ticks
	
	iEn: INT;					// parm to FB_PlusMinusByTime - enable of fountain used in FB, this should be a BOOL
	iActual: INT;					// parm to FB_PlusMinusByTime - do we need this????
	iScaleActualUnits: WORD;		// parm to FB_PlusMinusByTime - max value of units of the requested target (% ink sweep) - usually 99%
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
(************************************************************************************************
 *
 *   FUNCTION BLOCK	:FBInkSweep_Time
 *
 *   Author		:	Mark Colvin			Date Apr 20, 2018
 *
 *   Description:	Perform inker positioner control of Ink Sweep.  Adjust user controlled ink trim value while in manual mode 
 *
 *   Parameters	:	Global Variables for configuration parameters
 *
 *   Returns	:		changes to Global Variables and Global IO controls
 *
 *   History	:   20-Apr-2018, MarkC Created
 *
***************************************************************************************************)
//!!!!!!!!!!!!!!!!!!!!!!!!!! NEED TO REMOVE THE FEEDBACK CODE !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
//FB_PlusMinusWithFB_arr[1].iRequested	:= nScreenValceu;  //not sure what this is doing here

IF (GVL.mb_Output_Coils[GVL.ENABLEFULLINIT] = FALSE) THEN		//do not process while initializing PLC

	FOR Index := 0 TO (GVL.w_MAXFOUNTAINS) DO		//normal operations - loop ALL the configured fountains
		//Index := 9;  //debug a single fountain, one at a time, 0 to max-1
		Index_offset := 0;		//ftns 1 to 6, WORDRUNTIME only
		IF( Index > 5 )THEN
			Index_offset := GVL.SWPWTR_7_10_FTN_OFFSET;	//ftns 7 to 10, WORDRUNTIME only
		END_IF	

		bError[Index] := FALSE;				//clear local error flag for each fountain
		w_itrgt := GVL.SWEEPTRIM + (Index * GVL.WORDRUNTIMEGROUPSIZE);
		IF( GVL.mb_Output_Coils[GVL.ENABLEFOUNTAIN + Index] ) THEN				// check if fountain enabled,
			IF( GVL.mb_Output_Coils[GVL.InkSweep_OFF +(Index * GVL.BOOLRUNTIMEGROUPSIZE)] = FALSE ) THEN		//test if this inker sweep is NOT disabled with function OFF 
				IF( GVL.mb_Output_Coils[GVL.CancelInkerPositioner +(Index * GVL.BOOLRUNTIMEGROUPSIZE)] = FALSE ) THEN		//test disable inker positioner mtr outputs from slave
					//test current ink trim % request with last % request, move positioner if not same - keeps motors from moving during reset condition - RUN boolean forces another attempt
					IF(( GVL.mb_Output_Registers[w_itrgt] <> GVL.w_LastSweepTrim[Index]) OR GVL.mb_Output_Coils[GVL.InkSweepRUN +(Index * GVL.BOOLRUNTIMEGROUPSIZE)]) THEN
						IF( GVL_IO.InkInput[Index] > 0 ) THEN							// is FB circuit value valid, process normally
							//test current ink trim target % with current on-press % value, move positioner if not same - keeps the motors moving toward target
							w_icmpr := GVL.SWEEPTRIMONPRESS + Index_offset + (Index * GVL.WORDRUNTIMEGROUPSIZE);
							IF( GVL.mb_Output_Registers[w_itrgt] <> GVL.mb_Output_Registers[w_icmpr]) THEN
								IF (GVL.mb_Output_Coils[GVL.PLCerrorLastRequestInk+(Index * GVL.BOOLRUNTIMEGROUPSIZE)]) THEN
									bRESET[Index] := TRUE;								// set bool to clear error condition in PlusMinus FB
								END_IF
						
								FB_PlusMinusByTime_arr[Index](    //normal operations
								//FB_PlusMinusWithFB_arr(			//debug the PlusMinusWithFB FB with one index
									iEn					:= GVL.mb_Output_Coils[GVL.ENABLEFOUNTAIN + Index] , 	// need to be TRUE for activating any output of inker positioner
									iActual				:= INT_TO_WORD(GVL_IO.InkInput[Index]), 				// actual potentiometer value from FB circuit, 0 means missing, 1 to 32768 normal
									iRequested			:= GVL.mb_Output_Registers[w_itrgt], 			// Requested target value of inker positioner, in % sweep
									iRESET				:= bRESET[Index], 								// reset any fault seen previously
									iNrOfStuckCycles	:= GVL.POSTIONER_TIMEOUT, 						// number of PLC scan cycles until FB gives warning at 1ms PLC cycle time; 4000 = 4s
									iScaleActualMinVal	:= GVL.w_InkInputIntMin[Index], 				// min A2D value from FB circuit, regarded as ZERO % sweep
									iScaleActualMaxVal	:= GVL.w_InkInputIntMax[Index],					// maximum A2D value from pot circuit, regarded as MAX % sweep
									iScaleActualUnits	:= GVL.mb_Output_Registers[GVL.MAXSWEEPTRIM], 	// units of requested target(max target value requested)
									iSETUP_jerkIfClose	:= GVL.PULSE_WHEN_CLOSE_OPTION,  				// flag to tweak motor output to reach target value if close (if motors are fast)
									qInc				=> GVL_IO.InkMotorFwd[Index], 		// motor output BOOL for forward direction of inker positioner motor (positive)
									qDec				=> GVL_IO.InkMotorRvs[Index], 		//motor output BOOL for reverse direction of inker positioner motor	(negative)
									qWarningStuck		=> bError[Index],					// error flag for motor/FB stuck error
									qErrorCode			=> wError);								//Error code if error flag
								IF(bError[Index] = TRUE) THEN
									wError :=	(wError * 100) + Index+1;					//add the fountain index to the error code to log to AS
								ELSE
									GVL.mb_Output_Coils[GVL.Inker_RUNNING +(Index * GVL.BOOLRUNTIMEGROUPSIZE)] := TRUE;		//flag ink trim motor moving
								END_IF
								w_Actual := INT_TO_WORD(GVL_IO.InkInput[Index]);							//actual analog FeedBack value is captured here
								IF(	INT_TO_WORD(GVL_IO.InkInput[Index]) < GVL.w_InkInputIntMin[Index]) THEN	//if lower than minimum, don't go lower.
									GVL_IO.InkMotorRvs[Index] := FALSE;							//turn off that direction
									w_Actual := GVL.w_InkInputIntMin[Index];					//fudge actual value to be min limit
								END_IF		
							
								IF(	INT_TO_WORD(GVL_IO.InkInput[Index]) > GVL.w_InkInputIntMax[Index]) THEN	//if greater than maximum, don't go higher.
									GVL_IO.InkMotorFwd[Index] := FALSE;						//turn off that direction
									w_Actual := GVL.w_InkInputIntMax[Index];					//fudge actual value to be max limit
								END_IF		
					
								// last % sweep trgt        curr analog     min analog allowed            max sweep %			//calc current % trim value from curr analog value
								GVL.w_LastSweepTrim[Index] := (w_Actual - GVL.w_InkInputIntMin[Index]) * (GVL.mb_Output_Registers[GVL.MAXSWEEPTRIM]+1) / 
																	(GVL.w_InkInputIntMax[Index] - GVL.w_InkInputIntMin[Index]);	
																	// max analog allowed - min analog allowed

								w_halfpercent[Index] := REAL_TO_WORD(GVL.r_InkInput1PercentInt[Index] /2.0);   //number of A2D ticks for 0.5%
								w_target_ticks[Index] := REAL_TO_WORD(GVL.r_InkInput1PercentInt[Index]) * GVL.mb_Output_Registers[GVL.SWEEPTRIM + Index_offset + (Index * GVL.WORDRUNTIMEGROUPSIZE)]; 
								IF(	(GVL.w_LastSweepTrim[Index] = GVL.mb_Output_Registers[GVL.SWEEPTRIM + Index_offset + (Index * GVL.WORDRUNTIMEGROUPSIZE)]) ) OR //fudge the last trim setting ticks to +/- 0.5%
									( (w_Actual < (w_target_ticks[Index]+w_halfpercent[Index]) ) AND	// of the requested target or calc'd sweep value is EQUAL
									(w_Actual > (w_target_ticks[Index]-w_halfpercent[Index]) ) ) THEN
										GVL.w_LastSweepTrim[Index] := GVL.mb_Output_Registers[w_itrgt];	// last trim is now requested target trim
								END_IF
								
//								IF(	(GVL.w_LastSweepTrim[Index] = GVL.mb_Output_Registers[GVL.SWEEPTRIM + (Index * GVL.WORDRUNTIMEGROUPSIZE)]) OR //fudge the last trim setting to +/- 1%
//									(GVL.w_LastSweepTrim[Index] = GVL.mb_Output_Registers[GVL.SWEEPTRIM + (Index * GVL.WORDRUNTIMEGROUPSIZE)]+1) OR	// of the requested target, getting EQUAL
//									(GVL.w_LastSweepTrim[Index] = GVL.mb_Output_Registers[GVL.SWEEPTRIM + (Index * GVL.WORDRUNTIMEGROUPSIZE)]-1)) THEN
//										GVL.w_LastSweepTrim[Index] := GVL.mb_Output_Registers[w_itrgt];	// last trim is now requested target trim
//								END_IF
																								//we want current trim %, not the target trim %
								GVL.mb_Output_Registers[w_icmpr]:= GVL.w_LastSweepTrim[Index];  	// update ON-PRESS trim % to current inker location
								GVL.mb_Output_Coils[GVL.PLCerrorLastRequestInk+(Index * GVL.BOOLRUNTIMEGROUPSIZE)] := FALSE;	// reset bool to show AS no longer in error condition
							ELSE							//no move needed if trgt and on-press are same
								GVL_IO.InkMotorFwd[Index] := FALSE;										//reset BOTH direction outputs, no move, no error 
								GVL_IO.InkMotorRvs[Index] := FALSE;
								GVL.mb_Output_Coils[GVL.Inker_RUNNING +(Index * GVL.BOOLRUNTIMEGROUPSIZE)] := FALSE; 
								bError[Index] 			:= FALSE;
								wError 					:= 0;
							END_IF										// test the curr trgt% with curr on-press% value
						ELSE
							bError[Index] := 	TRUE;							//if enabled and zero feedback signal, this is an error.
							wError :=	(GVL.MISSINGFBSIGNAL * 100) + Index+1;
						END_IF										
					ELSE					// curr trgt% is same as last request%; no move required, no error
						GVL_IO.InkMotorFwd[Index] := FALSE;										//reset BOTH direction outputs, no move, no error 
						GVL_IO.InkMotorRvs[Index] := FALSE;
						GVL.mb_Output_Coils[GVL.Inker_RUNNING +(Index * GVL.BOOLRUNTIMEGROUPSIZE)] := FALSE; 
						bError[Index] 			:= FALSE;
						wError 					:= 0;
					END_IF
				ELSE						//cancel output is TRUE, stop the motor outputs now and read current on-press value, set oper request error
					bError[Index] := 	TRUE;							//oper request cancel, this is an error.
					wError :=		(GVL.INKPOSITCANCEL * 100) + Index+1;
					GVL.mb_Output_Coils[GVL.CancelInkerPositioner +(Index * GVL.BOOLRUNTIMEGROUPSIZE)] := FALSE;	//reset the output cancel bit here
				END_IF
				IF(bError[Index] ) THEN								//if error code from PlusMinusWithPot FB or FB signal missing or Cancel Output from slave
					FProcessError(ErrorNumber:= wError ); 				// Set Ink Input Feedback Error for AppServer to process. 
					bRESET[Index] := TRUE;										//reset this error indication for next fountain
					GVL.mb_Output_Coils[GVL.PLCerrorLastRequestInk+(Index * GVL.BOOLRUNTIMEGROUPSIZE)] := TRUE;		// set bool to show AS error condition
					GVL_IO.InkMotorFwd[Index] := FALSE;							//reset BOTH direction outputs, no move if error
					GVL_IO.InkMotorRvs[Index] := FALSE;
					GVL.mb_Output_Coils[GVL.Inker_RUNNING +(Index * GVL.BOOLRUNTIMEGROUPSIZE)] := FALSE; 
					GVL.w_LastSweepTrim[Index] := GVL.mb_Output_Registers[w_itrgt];	// last trim is now requested target trim
				ELSE
					bRESET[Index] := FALSE;													//no error, reset error indication for next fountain anyway
				END_IF	
				GVL.mb_Output_Coils[GVL.InkSweepRUN +(Index * GVL.BOOLRUNTIMEGROUPSIZE)] := FALSE;	//not running now
			ELSE				//if ftn disabled with OFF function
				GVL_IO.InkMotorFwd[Index] := FALSE;							//reset BOTH direction outputs, not moving if function is OFF
				GVL_IO.InkMotorRvs[Index] := FALSE;
				GVL.mb_Output_Coils[GVL.Inker_RUNNING +(Index * GVL.BOOLRUNTIMEGROUPSIZE)] := FALSE; 
			END_IF   // test disable inker sweep
		ELSE				//if ftn not enabled
			GVL_IO.InkMotorFwd[Index] := FALSE;							//reset BOTH direction outputs, not moving if not enabled
			GVL_IO.InkMotorRvs[Index] := FALSE;
			GVL.mb_Output_Coils[GVL.Inker_RUNNING +(Index * GVL.BOOLRUNTIMEGROUPSIZE)] := FALSE; 
		END_IF 	
	END_FOR 	//normal operations - loop all fountains here
END_IF		//not initializing PLC test
	]]></ST>
    </Implementation>
    <LineIds Name="FBInkSweep_Time">
      <LineId Id="3" Count="137" />
      <LineId Id="2" Count="0" />
      <LineId Id="142" Count="3" />
    </LineIds>
  </POU>
</TcPlcObject>